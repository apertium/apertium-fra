DELIMITERS = "<.>" "<!>" "<?>" "<..>" "<...>" "<¶>" ;
SOFT-DELIMITERS = "<,>" ;
OPTIONS += no-inline-sets no-inline-templates ;

SETS

LIST BOS = (>>>) ;
LIST EOS = (<<<) ;

LIST Verb = vblex vbser vbhaver vbmod ;
LIST V = vblex ;
LIST Pron = prn ;
LIST Haver = vbhaver ;
LIST Ser = vbser ;
LIST Vbmod = vbmod ;
LIST Cm = cm ;
LIST Det = det ;
LIST Def = def ;
LIST Prep = pr ;
LIST Subj = subj ;
LIST Prop = np ;
LIST A = adj ;
LIST Adv = adv ;
LIST Preadv = preadv ;
LIST Cnjadv = cnjadv ;
LIST Num = num ;
LIST Prs = prs ;
LIST Pro = pro ;
LIST Tn = tn ;
LIST Imper = imp ;
LIST Pri = pri ;
LIST Prn = prn ;
LIST N = n ;

LIST Interr = itg ;
LIST Rel = rel ;
LIST Interj = ij ;

LIST Sg = sg ;
LIST Pl = pl ;
LIST Sp = sp ;

LIST Masc = m ;
LIST Fem = f ;

LIST P1 = p1 ;
LIST P2 = p2 ;
LIST P3 = p3 ;
LIST Sg1 = (p1 sg) ;
LIST Sg2 = (p2 sg) ;
LIST VSg1 = (vblex p1 sg) (vbser p1 sg) (vbhaver p1 sg) (vbmod p1 sg);
LIST VSg2 = (vblex p2 sg) (vbser p2 sg) (vbhaver p2 sg) (vbmod p2 sg);
LIST VSg3 = (vblex p3 sg) (vbser p3 sg) (vbhaver p3 sg) (vbmod p3 sg);
LIST VPl1 = (vblex p1 pl) (vbser p1 pl) (vbhaver p1 pl) (vbmod p1 pl);
LIST VPl2 = (vblex p2 pl) (vbser p2 pl) (vbhaver p2 pl) (vbmod p2 pl);
LIST VPl3 = (vblex p3 pl) (vbser p3 pl) (vbhaver p3 pl) (vbmod p3 pl);

LIST Fin = pri prs pii ifi pis cni imp ;
LIST Inf = inf ;
LIST PrfPrc = pp ;

LIST Que = "que" ;
LIST Qui = "qui" ;
LIST Avoir = "avoir" ; 
LIST Le = "le" ;
LIST Fait = "<fait>"i ;
LIST Est = "<est>"i ;
LIST Je = "je" ;
LIST Tu = "tu" ;
LIST Il = "il" ;
LIST Nous = "nous" ;
LIST Vous = "vous" ;
LIST Lui = "lui" ;
LIST Eux = "eux" ;
LIST En = "en" ; 
LIST Pas = "pas" ; 
LIST Ne = "ne" ; 
LIST Aussi = "aussi" ; 
LIST Même = "même" ; 
LIST Devoir = "devoir" ;
LIST Être = "être" ;

LIST Fil = "fil" ;
LIST Fils = "fils" ;
LIST Foi = "foi" ;
LIST Fois = "fois" ;
LIST Mille = "mille" ;
LIST Mil = "mil" ;
LIST Somme = "somme" ;
LIST Peu = "peu de" ;
LIST Helas = "hélas" ;
LIST Des = "dès" ;
LIST Entre = "entre" ;
LIST Si = "si" ;
LIST S = "<s'>"i ;

LIST À = "à" ;

LIST Excl = "!" ;


LIST Title = "<[A-Z][a-z]\+>"r ;

SET QUI-QUE = Que | Qui ;

SET CLB = Subj | Cm ;

SECTION

SELECT Prop IF (NOT -1 BOS) (0 Title) ;
  ## Lorsque Marie a fini de compter, elle regarde autour d'elle.

REMOVE Prs IF (NOT -1* QUI-QUE BARRIER CLB); 

REMOVE Pro IF (0 Le) (NOT 1 V) ;
  ## Le chien aime jouer avec les deux enfants.

REMOVE N IF (0 Fait) (-1 Il) ;
  ## Il fait beau aujourd'hui, il fait très chaud.

REMOVE N IF (0 Est) (-1 Interr OR Rel) ;
  ## Où est Jean ?

SELECT Adv IF (0 Pas) (-1 V LINK -1 Ne) ;
  ## Ils n'ont pas pu jouer dehors.

REMOVE Fin IF (0 Fin OR Prop) (0 Title) (1C Fin) ;
  ## Marie pense qu'elle a vu où Jean est allé :


SELECT Inf IF (0 Être) (-1 Vbmod) ;
  ## Ce doit être Jean ! 

SELECT Inf IF (0 Être) (-1 Pro) (-2 Vbmod) ;
  ## Le spécialiste lorrain du 10 kilomètres peut l'être.

SELECT Haver IF (0 Avoir) (1 PrfPrc) ;
  ## Est-ce que tu as vu Jean ?

SELECT Haver IF (0 Avoir) (2 PrfPrc) ;
  ## As-tu vu Jean ?
  
SELECT V IF (0 Avoir) (1C Prep) ;
  ## ayant pour ami

SELECT V IF (0 Avoir) (1 EOS) ;
  ## quels professeurs on va avoir.
## SELECT N IF (0 Avoir) (1 EOS) (-1 Num);	## CAL TREBALLAR-HI!
  ## 2 A.
## SELECT N IF (0 Avoir) (1 EOS) (-1 BOS);	## CAL TREBALLAR-HI!
  ## 2. A.

SELECT Prep IF (-1 BOS) (0 Avoir) (0 À) ;
  ## A la téle
  
REMOVE Imper IF (NOT 1* Excl) ;
REMOVE Imper IF (-1 Prn) ;

SELECT Fin IF (0C N OR Fin) (-1 N LINK -1 Prep OR Det) (NOT 1 N OR A) ;
  ## Les poules du couvent couvent

SELECT Fils IF (0C Fil OR Fils) (-1 Sg) ;
  ## Avec lequel elle aura un fils.

SELECT Fois IF (0C Foi OR Fois) ;

SELECT Mil IF (0C Mil OR Mille) (1 N OR A) (1 Pl OR Sp) ;
  ## mille adhérents


SELECT Tn IF (0 Lui) (1 Aussi);
SELECT Tn IF (0 Nous) (1 Aussi);
SELECT Tn IF (0 Vous) (1 Aussi);
SELECT Tn IF (0 Lui) (1 Même);
SELECT Tn IF (0 Nous) (1 Même + Pl);
SELECT Tn IF (0 Vous) (1 Même + Pl);
SELECT Tn IF (0 Lui) (-1C Prep) (NOT 1 Inf);
SELECT Tn IF (0 Nous) (-1C Prep) (NOT 1 Inf);
SELECT Tn IF (0 Vous) (-1C Prep) (NOT 1 Inf);
SELECT Pro IF (0 Lui) (-1C Prep) (1 Inf);
SELECT Pro IF (0 Nous) (-1C Prep) (1 Inf);
SELECT Pro IF (0 Vous) (-1C Prep) (1 Inf);

SELECT Tn IF (0 Tu) (1 VSg2);
  ## Tu veux
SELECT Tn IF (0 Tu) (1 Ne) (2 VSg2);
  ## Tu ne veux pas
SELECT Tn IF (0 Tu) (1 Pro) (2 VSg2);
  ## Tu lui veux
SELECT Tn IF (0 Tu) (1 Pro) (2 Pro) (3 VSg2);
  ## Tu lui en veux
SELECT Tn IF (0 Tu) (1 Ne) (2 Pro) (3 Pro) (4 VSg2);
  ## Tu ne lui veux pas
SELECT Tn IF (0 Tu) (1 Ne) (2 Pro) (3 VSg2);
  ## Tu ne lui veux pas

SELECT Pro IF (-1 Je) (0 Pro) (1 VSg1);
  ## Je vous parle
SELECT Pro IF (-2 Je) (-1 Ne) (0 Pro) (1 VSg1);
  ## Je ne vous parle pas
SELECT Pro IF (-1 Je) (0 Pro) (1 Pro) (2 VSg1);
  ## Je vous en parle
SELECT Pro IF (-2 Je) (-1 Ne) (0 Pro) (1 Pro) (2 VSg1);
  ## Je ne vous en parle pas
SELECT Pro IF (-1 Tu) (0 Pro) (1 VSg2);
  ## Tu nous parles
SELECT Pro IF (-2 Tu) (-1 Ne) (0 Pro) (1 VSg2);
  ## Tu ne nous parles pas
SELECT Pro IF (-1 Tu) (0 Pro) (1 Pro) (2 VSg2);
  ## Tu nous en parles
SELECT Pro IF (-2 Tu) (-1 Ne) (0 Pro) (1 Pro) (2 VSg2);
  ## Tu ne nous en parles pas
SELECT Pro IF (-1 Il + Sg) (0 Pro) (1 VSg3);
  ## Elle vous parle
SELECT Pro IF (-2 Il + Sg) (-1 Ne) (0 Pro) (1 VSg3);
  ## Elle ne vous parle pas
SELECT Pro IF (-1 Il + Sg) (0 Pro) (1 Pro) (2 VSg3);
  ## Elle vous en parle
SELECT Pro IF (-2 Il + Sg) (-1 Ne) (0 Pro) (1 Pro) (2 VSg3);
  ## Elle ne vous en parle pas
SELECT Pro IF (-1 Nous) (0 Pro) (1 VPl1);
  ## Nous vous parlons
SELECT Pro IF (-2 Nous) (-1 Ne) (0 Pro) (1 VPl1);
  ## Nous ne vous parlons pas
SELECT Pro IF (-1 Nous) (0 Pro) (1 Pro) (2 VPl1);
  ## Nous vous en parlons
SELECT Pro IF (-2 Nous) (-1 Ne) (0 Pro) (1 Pro) (2 VPl1);
  ## Nous ne vous en parlons pas
SELECT Pro IF (-1 Vous) (0 Pro) (1 VPl2);
  ## Vous nous parlez
SELECT Pro IF (-2 Vous) (-1 Ne) (0 Pro) (1 VPl2);
  ## Vous ne nous parlez pas
SELECT Pro IF (-1 Vous) (0 Pro) (1 Pro) (2 VPl2);
  ## Vous nous en parlez
SELECT Pro IF (-2 Vous) (-1 Ne) (0 Pro) (1 Pro) (2 VPl2);
  ## Vous ne nous en parlez pas
SELECT Pro IF (-1 Il + Pl) (0 Pro) (1 VPl3);
  ## Elles vous parlent
SELECT Pro IF (-2 Il + Pl) (-1 Ne) (0 Pro) (1 VPl3);
  ## Elles ne vous parlent pas
SELECT Pro IF (-1 Il + Pl) (0 Pro) (1 Pro) (2 VPl3);
  ## Elles vous en parlent
SELECT Pro IF (-2 Il + Pl) (-1 Ne) (0 Pro) (1 Pro) (2 VPl3);
  ## Elles ne vous en parlent pas


SELECT P1 IF (-1 Je) (0 VSg1);
  ## Je veux
SELECT P1 IF (-2 Je) (-1 Ne) (0 VSg1);
  ## Je ne veux pas
SELECT P1 IF (-2 Je) (-1 Pro) (0 VSg1);
  ## Je lui veux
SELECT P1 IF (-3 Je) (-2 Ne) (-1 Pro) (0 VSg1);
  ## Je ne lui veux pas
SELECT P1 IF (-3 Je) (-2 Pro) (-1 Pro) (0 VSg1);
  ## Je lui en veux
SELECT P1 IF (-4 Je) (-3 Ne) (-2 Pro) (-1 Pro) (0 VSg1);
  ## Je ne lui en veux pas
SELECT P2 IF (-1 Tu) (0 VSg2);
  ## Tu veux
SELECT P2 IF (-2 Tu) (-1 Ne) (0 VSg2);
  ## Tu ne veux pas
SELECT P2 IF (-2 Tu) (-1 Pro) (0 VSg2);
  ## Tu lui veux
SELECT P2 IF (-3 Tu) (-2 Ne) (-1 Pro) (0 VSg2);
  ## Tu ne lui veux pas
SELECT P2 IF (-3 Tu) (-2 Pro) (-1 Pro) (0 VSg2);
  ## Tu lui en veux
SELECT P2 IF (-4 Tu) (-3 Ne) (-2 Pro) (-1 Pro) (0 VSg2);
  ## Tu ne lui en veux pas
SELECT P3 IF (-1 Il + Sg) (0 VSg3);
  ## Elle parle
SELECT P3 IF (-2 Il + Sg) (-1 Ne) (0 VSg3);
  ## Elle ne parle pas
SELECT P3 IF (-2 Il + Sg) (-1 Pro) (0 VSg3);
  ## Elle lui parle
SELECT P3 IF (-3 Il + Sg) (-2 Ne) (-1 Pro) (0 VSg3);
  ## Elle ne lui parle pas
SELECT P3 IF (-3 Il + Sg) (-2 Pro) (-1 Pro) (0 VSg3);
  ## Elle lui en parle
SELECT P3 IF (-4 Il + Sg) (-3 Ne) (-2 Pro) (-1 Pro) (0 VSg3);
  ## Elle ne lui en parle pas

SELECT Tn IF (NOT -1 Prn) (0 Nous) (1 VPl1);
  ## parce que nous parlons
SELECT Tn IF (NOT -1 Prn) (0 Vous) (1 VPl2);
  ## parce que vous parlez

SELECT PrfPrc IF (-1 Haver) (0 PrfPrc) ;
  ## Vous avez dit
SELECT Sg IF (-1 Haver) (0 PrfPrc + Sg OR Pl) ; 
  ## Vous avez pris
SELECT PrfPrc IF (-3 Ne) (-2 Haver) (0 PrfPrc) ;
  ## Vous n'avez pas dit
SELECT Sg IF (-3 Ne) (-2 Haver) (0 PrfPrc + Sg OR Pl) ;
  ## Vous n'avez pas pris
SELECT PrfPrc IF (-4 Ne) (-3 Pro) (-2 Haver) (0 PrfPrc) ;
  ## Vous ne lui avez pas dit
SELECT Sg IF (-4 Ne) (-3 Pro) (-2 Haver) (0 PrfPrc + Sg OR Pl) ;
  ## Vous ne lui avez pas pris
SELECT PrfPrc IF (-5 Ne) (-4 Pro) (-3 Pro) (-2 Haver) (0 PrfPrc) ;
  ## Vous ne le lui avez pas dit
SELECT Sg IF (-5 Ne) (-4 Pro) (-3 Pro) (-2 Haver) (0 PrfPrc + Sg OR Pl) ;
  ## Vous ne le lui avez pas pris

SELECT PrfPrc IF (-1 Ser) (0 PrfPrc) (OC Verb) ;
  ## sont investis

REMOVE N IF (-1 Nous) (0 Somme OR Ser) ;
  ## nous sommes assaillis

SELECT Prep IF (0 En) (1C N OR A) ;

SELECT N IF (0 N OR A) (-1C Peu) (1 EOS) ;
	  ## peu de joueurs.
SELECT A IF (0 N OR A) (-1C Peu) (1C N) ;

SELECT Interj IF (0 Helas) ;
  ## triem ser hélas com a interjecció

SELECT Adv IF (-1 Des) (0 Verb OR Adv) ;
  ## dès maintenant

SELECT Prep IF (0 Entre) (1 Nous OR Vous OR Eux);
  ## entre nous, vous, eux, elles

SELECT Prep IF (0 Entre) (1 Il + Pl + Fem);
  ## entre elles

SELECT Preadv IF (0 Si) (1 Adv OR Preadv);
  ## si bien / si mal

SELECT Adv IF (-1 Si) (0 Adv OR Preadv);
  ## si bien / si mal

SELECT Prn IF (0 S) (NOT 1 Il) ;
SELECT Cnjadv IF (0 S) (1 Il) ;
  ## s'affirme

SELECT Adv IF (-1 BOS) (0 Adv OR A) (1C Prep) ;
  ## Même sur le terrain
